REFINEMENT
    Linkage_Synchronization
REFINES
    Abstract_Operations
INCLUDES
    Lift_Existing, liftM
DEFINITIONS
    SET_PREF_SHOW_EVENTB_ANY_VALUES == TRUE ;
    SET_PREF_MAXINT == 10

     
    
PROPERTIES
    /*FLOOR: FIN(FLOOR) &*/
	groundf = 1 & /* Set to 1. */
	topf = card(FLOOR)  /* Set to cardinality of DSL-Spec. */
	

VARIABLES
	
	  /* Variable to link floors. */
	linked_lift,
	linked_floors
INVARIANT	
	
	/*& ran(linked_floors) = groundf..topf  hand written*/
	

	linked_lift : Lift /* generated */
	
	& linked_floors :  Floor >->>  groundf..topf /* generated */ 
	
	& !(f1,f2).(f1 : Floor & f2 : Floor & (f1|->f2) : up_down => linked_floors(f2) = linked_floors(f1) + 1)  /*hand written*/
	
    & (Door(linked_lift) = Closed => door_open = FALSE)
    & (Door(linked_lift) = Open => door_open = TRUE)
    
    & (Direction(linked_lift) = Down => direction_up = FALSE)
    & (Direction(linked_lift) = Up => direction_up = TRUE)

    & linked_floors[selectedFloors[{linked_lift}]] = call_buttons
    
    & linked_floors(liftPosition(linked_lift)) = cur_floor /*generated*/

INITIALISATION
    

	
	linked_lift::Lift ; /* generated */

	ANY mapFloors WHERE 
		mapFloors : Floor >->>  groundf..topf  &
		!(f1,f2).(f1 : Floor & f2 : Floor & (f1|->f2) : up_down => mapFloors(f2) = mapFloors(f1) + 1)  
	THEN
		linked_floors := mapFloors
	END /* generated */ ; 
	
	/*ANY cur_floor_, door_open_, direction_up_, call_buttons_ WHERE
	    cur_floor_ = linked_floors(liftPosition(linked_lift))
	    & (Door(linked_lift) = Closed => door_open_ = FALSE)
	    & (Door(linked_lift) = Open => door_open_ = TRUE)
	    & (Direction(linked_lift) = Down => direction_up_ = FALSE)
	    & (Direction(linked_lift) = Up => direction_up_ = TRUE)
	    & linked_floors[selectedFloors[{linked_lift}]] = call_buttons_
	THEN 
	    setValues(call_buttons_, cur_floor_,direction_up_,door_open_) 
	END*/
	IF
        	liftPosition(linked_lift) /= linked_floors~(cur_floor)
    	THEN
	    	SetLiftPosition(linked_lift,linked_floors~(cur_floor))
	END;
	SetDirection(linked_lift ,Up);
	SetDoor(linked_lift,Closed);
	UnsetSelectedFloors(linked_lift)
	
OPERATIONS
move_up_inc = 
BEGIN
    move_up ;
    SetLiftPosition(linked_lift,linked_floors~(cur_floor))
END;
move_down_inc = 
BEGIN
    move_down ;
    SetLiftPosition(linked_lift,linked_floors~(cur_floor))
END;
reverse_lift_up_inc =
BEGIN
    reverse_lift_up;
    SetDirection(linked_lift ,Up)
END;
reverse_lift_down_inc =
BEGIN
    reverse_lift_down;
    SetDirection(linked_lift, Down)
END;
open_door_inc =
BEGIN
    open_door;
    SetDoor(linked_lift, Open)
END;
close_door_inc =
BEGIN
    close_door;
    SetDoor(linked_lift,Closed) ;
    RemoveSelectedFloors(linked_lift, linked_floors~(cur_floor))
END;

push_call_button_inc(fl)=
PRE 
    fl : (groundf .. topf)
    & fl /: call_buttons
THEN
    push_call_button(fl);
    AddSelectedFloors(linked_lift, linked_floors~(fl))
END;

select_lift =
ANY 
    lift
WHERE
    lift : LIFT & lift /= linked_lift
THEN 
    linked_lift := lift ;
    ANY cur_floor_, door_open_, direction_up_, call_buttons_ WHERE
	    cur_floor_ = linked_floors(liftPosition(linked_lift))
	    & (Door(linked_lift) = Closed => door_open_ = FALSE)
	    & (Door(linked_lift) = Open => door_open_ = TRUE)
	    & (Direction(linked_lift) = Down => direction_up_ = FALSE)
	    & (Direction(linked_lift) = Up => direction_up_ = TRUE)
	    & linked_floors[selectedFloors[{linked_lift}]] = call_buttons_
	THEN 
	    setValues(call_buttons_, cur_floor_,direction_up_,door_open_) 
	END
END


END

